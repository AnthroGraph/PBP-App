{{#> Help:StartFrameTocUi tocItemId="Patholist"}}

<div class="page" style="margin-left:30px; margin-right: 30px">

<h3>Create New PBP-Paleopathology Instance</h3>
<semantic-form
for-class="http://w3id.org/rdfbones/ext/phaleron-patho/PaleopathologyInvestigation" 
new-subject-template="http://pbp-paleoppath.org/records/Paleopath-{{UUID}}"
post-action='redirect'
persistence='sparql'

fields='[{
    "id": "ofType",
    "label": "Skeletal Inventory",
    "description": "Select a Skeletal Inventory",
    "xsdDatatype": "xsd:anyURI",
    "maxOccurs": 1,
    "minOccurs": 1,
    "insertPattern": "
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX vivo: <http://vivoweb.org/ontology/core#>
PREFIX phaleron-patho:<http://w3id.org/rdfbones/ext/phaleron-patho/>
PREFIX cidoc:<http://www.cidoc-crm.org/cidoc-crm/>
PREFIX dc: <http://purl.org/dc/terms/>
INSERT {GRAPH ?mygraph {  
      
      $subject rdfs:label ?mylabel .
      $subject a <http://w3id.org/rdfbones/ext/phaleron-patho/PhaleronPaleopathologyDataset> .
      ?myPPprocess obo:OBI_0000299 $subject .
      ?myPPprocess a <http://w3id.org/rdfbones/ext/phaleron-patho/PaleopathologyInvestigation> .
      ?myPPprocess obo:OBI_0000293 $value .
      $subject dc:date \"[[currentDateTime format="yyyy-MM-dd"]]\"^^xsd:date . 
      }
} WHERE {
    
    BIND(URI(CONCAT(STR(?subject),\"-graph\")) AS ?mygraph ) .
    
    ?mySI <http://semanticscience.org/resource/SIO_000563> $value  .
    ?mySI rdfs:label ?label .
    
    BIND(URI(CONCAT(STR(?subject),\"_PBP_Patho_PaleopathDataset\")) AS ?myPPprocess) .
    BIND(CONCAT(STR(?label),\"_PBP_Patho_Paleopath\") AS ?mylabel) .
   
}"
,


"deletePattern": "
    PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
    PREFIX obo: <http://purl.obolibrary.org/obo/>
    PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
    DELETE {$subject obo:OBI_0000293 $value .} WHERE {$subject obo:OBI_0000293 $value .} ",


"autosuggestionPattern": "
                PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
                PREFIX obo: <http://purl.obolibrary.org/obo/>
                SELECT $value $label WHERE {
                      ?si a <http://w3id.org/rdfbones/ext/phaleron-si/InventoryForAdultSkeletons>.
                      ?si rdfs:label $label .
                      ?si <http://semanticscience.org/resource/SIO_000563> $value
                  FILTER REGEX($label, $__token__, \"i\")
                }
              ",

"constraints": [{
                 "validatePattern":"
                         PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
                         PREFIX obo: <http://purl.obolibrary.org/obo/>
                         PREFIX phaleron-patho:<http://w3id.org/rdfbones/ext/phaleron-patho/>
                         ASK WHERE { FILTER NOT EXISTS {
   									         	?value a <http://w3id.org/rdfbones/core#HumanSkeletonSpecimen> .
    								          ?paleopath obo:OBI_0000293 ?value.
  				                    ?paleopath a <http://w3id.org/rdfbones/ext/phaleron-patho/PaleopathologyInvestigation> .                              
  											    }}",
                  "message": "This Skeleton already is associated with a Phaleron Paleopath Instance"
                  }]
    }]'>

<semantic-form-autocomplete-input for='ofType'></semantic-form-autocomplete-input>
    <button name="submit" class="btn btn-primary"><strong>Save</strong></button>
    <button name="reset" class="btn btn-secondary">Reset</button>
</semantic-form>

<br><br>

<mp-sparql-download query="
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX standards-si: <http://w3id.org/rdfbones/ext/standards-si/>
PREFIX dc: <http://purl.org/dc/terms/>

SELECT DISTINCT 

?ID
?SectionName
?SectionType
?SectionROI
?MeasurementDatumType
?MeasurementDatumROI
?Value

?ResearchRoleType
?ResearchRoleContributor
?ContributionDate
?InventoryComment
?GeneralAnatomicalRegionComment
?SpecificPathologyComment
?GeneralContributionComment
?PathologyObservationContributionComment

 WHERE {
  ?subject a <http://w3id.org/rdfbones/ext/phaleron-patho/PhaleronPaleopathologyDataset> .
  ?subject rdfs:label ?ID .
  {
    { ?subject rdfs:comment ?InventoryComment 
    }
    UNION { 
      ?subject obo:BFO_0000051 ?section .
      ?section a ?sectiontype .
      ?sectiontype rdfs:label ?SectionType .
      ?sectiontype rdfs:subClassOf <http://w3id.org/rdfbones/ext/phaleron-patho/PathologyComments> .
      ?section rdfs:comment ?GeneralAnatomicalRegionComment .
    }
    UNION {
    ?subject obo:BFO_0000051 ?section .
      OPTIONAL { ?section rdfs:label ?SectionName }
    ?section a ?sectiontype .
      OPTIONAL { ?section rdfs:comment ?SpecificPathologyComment }
    VALUES ?sectiontype { <http://w3id.org/rdfbones/ext/phaleron-patho/NonMetricTraitsAndAnomaliesDatasetSection> <http://w3id.org/rdfbones/ext/phaleron-patho/BoneFusionDatasetSection> }
    ?sectiontype rdfs:label ?SectionType .
    ?section obo:IAO_0000136 ?SectionROIInst . 
    ?SectionROIInst a ?SectionROIType .
    ?SectionROIType a owl:Class .
      OPTIONAL { ?SectionROIType rdfs:label ?SectionROI }  }
  UNION { 
    ?subject obo:BFO_0000051 ?section .
      OPTIONAL { ?section rdfs:comment ?SpecificPathologyComment }
      OPTIONAL { ?section rdfs:label ?SectionName }
    ?section a ?sectiontype .
    ?sectiontype rdfs:label ?SectionType .
    ?section obo:IAO_0000136 ?SectionROIInst . 
    ?SectionROIInst a ?SectionROIType .
    ?SectionROIType a owl:Class .
      { {?SectionROIInst <http://purl.org/sig/ont/fma/regional_part_of> ?region }
        UNION
        { ?SectionROIType rdfs:subClassOf <http://w3id.org/rdfbones/anthrograph/app/phaleron-app/AnatomicalROIGroup> }
      }
    ?SectionROIType rdfs:label ?SectionROI .
    ?section obo:BFO_0000051 ?Tier2MD .
	?Tier2MD a ?Tier2MDType .
      OPTIONAL { ?Tier2MDType rdfs:label ?MeasurementDatumType }
      OPTIONAL { ?Tier2MD obo:IAO_0000136 ?Tier2MDROI .
        		?Tier2MDROI a ?Tier2MDROIType .
      			?Tier2MDROIType rdfs:label ?MeasurementDatumROI }
    ?Tier2MD obo:OBI_0001938 ?Tier2VS .
    ?Tier2VS obo:OBI_0000999 ?value .
      OPTIONAL { ?value rdfs:label ?Value }
    }
    UNION { 
      ?subject obo:BFO_0000051 ?SizeAndShapeDataset .
      OPTIONAL { ?SizeAndShapeDataset rdfs:comment ?SpecificPathologyComment }
      OPTIONAL { ?SizeAndShapeDataset rdfs:label ?SectionName }
      ?SizeAndShapeDataset obo:BFO_0000051 ?SizeAndShapeROI .
      ?SizeAndShapeDataset a ?sectiontype .
      ?sectiontype rdfs:label ?SectionType .
  	  ?SizeAndShapeROI a ?AnatomicalRegion .
      ?AnatomicalRegion rdfs:label ?MeasurementDatumROI.
      ?AnatomicalRegion rdfs:subClassOf ?ROISpecificationClass .
  	  ?ROISpecificationClass a owl:Class .
 	  ?ROISpecificationClass rdfs:subClassOf <http://w3id.org/rdfbones/ext/phaleron-patho/RegionalAROISpecification> .
      ?AnatomicalRegion rdfs:label ?SectionROI .
      ?SizeAndShapeDataset obo:BFO_0000051 ?Tier2MD .
      ?Tier2MD a ?Tier2MDType .
      OPTIONAL { ?Tier2MDType rdfs:label ?MeasurementDatumType }
      ?Tier2MD obo:OBI_0001938 ?Tier2VS .
      ?Tier2VS obo:OBI_0000999 ?value .
      OPTIONAL { ?value rdfs:label ?Value }
      }
    UNION {
    ?subject obo:BFO_0000051 ?section .
      OPTIONAL { ?section rdfs:comment ?SpecificPathologyComment }
      OPTIONAL { ?section rdfs:label ?SectionName }
    ?section a ?sectiontype .
    ?sectiontype rdfs:label ?SectionType .
    ?section obo:IAO_0000136 ?SectionROIInst .
      OPTIONAL { ?SectionROIInst rdfs:label ?SectionROI }
    ?section obo:BFO_0000051 ?Tier2MD .
  	?Tier2MD obo:IAO_0000136 ?Tier2MDROI .
    ?Tier2MDROI a ?Tier2MDROIType .
    ?Tier2MDROIType rdfs:label ?MeasurementDatumROI .
    ?Tier2MD obo:BFO_0000051 ?Tier3MD .
    ?Tier3MD a ?Tier3MDType .
      OPTIONAL { ?Tier3MDType rdfs:label ?MeasurementDatumType }
    ?Tier3MD obo:OBI_0001938 ?Tier3VS .
    ?Tier3VS obo:OBI_0000999 ?value .
      OPTIONAL { ?value rdfs:label ?Value }
    }
  UNION
  {
?investigation obo:OBI_0000299 ?subject .
?investigation obo:BFO_0000051 ?ContribProcess .
?ContribProcess a <http://w3id.org/rdfbones/ext/phaleron-patho/ResearchContributionProcess> .
?ContribProcess rdfs:label ?TypeOfContribution .
?ResearchRole obo:BFO_0000054 ?ContribProcess .
?ResearchRole a ?RoleType .
?RoleType rdfs:label ?ResearchRoleType .
?ContribProcess obo:RO_0000057 ?participant .
?participant rdfs:label ?ResearchRoleContributor .
?ContribProcess dc:date ?ContributionDate .
	OPTIONAL {?ContribProcess rdfs:comment ?GeneralContributionComment } 
  }
  UNION
  {
?investigation obo:OBI_0000299 ?subject .
?subject obo:BFO_0000051 ?section.
?section rdfs:label ?SectionName .
?investigation obo:BFO_0000051 ?PathoObservation .
?PathoObservation a ?PathoObservationType .
?PathoObservationType rdfs:subClassOf <http://w3id.org/rdfbones/ext/phaleron-patho/PaleopathologicalObservation> .
?PathoObservation obo:OBI_0000299 ?section .
?PathoObservation obo:BFO_0000051 ?ContribProcess .
?ContribProcess a <http://w3id.org/rdfbones/ext/phaleron-patho/ResearchContributionProcess> .
?ContribProcess rdfs:label ?TypeOfContribution .
?ResearchRole obo:BFO_0000054 ?ContribProcess .
?ResearchRole a ?RoleType .
?RoleType rdfs:label ?ResearchRoleType .
?ContribProcess obo:RO_0000057 ?participant .
?participant rdfs:label ?ResearchRoleContributor .
?ContribProcess dc:date ?ContributionDate .
	OPTIONAL { ?ContribProcess rdfs:comment ?PathologyObservationContributionComment }
  }

}
}
ORDER BY ?ID ?SectionName ?SectionType ?MeasurementDatumType
"
header="text/csv" filename="Paleopath_Investigations.csv">
  <button class="btn btn-secondary"><strong>Download CSV Table of all Paleopathology Investigations</i></strong></button>
</mp-sparql-download>  

<br><br>

<h3>Show all available Paleopath Instances:</h3></hr>
<semantic-table 
  query="
      PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
      PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
      PREFIX obo: <http://purl.obolibrary.org/obo/>
      SELECT ?paleodataset ?value WHERE { 
             ?paleodataset a <http://w3id.org/rdfbones/ext/phaleron-patho/PhaleronPaleopathologyDataset> .
             ?paleoprocess obo:OBI_0000299 ?paleodataset .
             ?paleoprocess a <http://w3id.org/rdfbones/ext/phaleron-patho/PaleopathologyInvestigation> .
             ?paleoprocess obo:OBI_0000293 ?specimen  .
             ?value <http://semanticscience.org/resource/SIO_000563> ?specimen .
             
}  "
  column-configuration='[
    {"variableName": "paleodataset", "displayName": "Paleopath Investigation"},
    {"variableName": "value", "displayName": "Referenced Skeletal Inventory"}
  ]'
></semantic-table>
</div>
{{/Help:StartFrameTocUi}}
